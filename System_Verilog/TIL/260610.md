## 목차
- [Class](#class)
		- [Q. Class 왜 쓸까](#q-class-왜-쓸까)
	- [property + method = member](#property--method--member)
	- [Class Vs Module](#class-vs-module)
	- [Class 사용](#class-사용)
		- [new()](#new)
	- [this(keyword)](#thiskeyword)
	- [local(keyword)](#localkeyword)
		- [function을 사용한 access(argument로 받음)](#function을-사용한-accessargument로-받음)
	- [⭐Handle 할당(Shallow Copy)](#handle-할당shallow-copy)
	- [static](#static)
	- [const(keyword)](#constkeyword)
	- [Parameterized Class](#parameterized-class)
		- [기존 코드](#기존-코드)
		- [파라미터화한 Class](#파라미터화한-class)
	- [typedef class](#typedef-class)
	- [::](#)
	- [Package](#package)
- [Randomization](#randomization)
	- [randomize()](#randomize)
		- [rand](#rand)
		- [randc](#randc)
		- [예시](#예시)
	- [Constraint](#constraint)
		- [dist(keyword)](#distkeyword)
		- [Array Constraint](#array-constraint)
		- [Implication \& Order constraint](#implication--order-constraint)
	- [System Function](#system-function)
		- [](#-1)
	- [Solver Order](#solver-order)
	- [Inline constraint](#inline-constraint)
	- [Soft Constraint](#soft-constraint)
		- [soft(keyword)](#softkeyword)
		- [다 soft인 경우](#다-soft인-경우)
	- [Mutual Constraint](#mutual-constraint)
		- [Error Case](#error-case)
	- [randomize() 부를 때](#randomize-부를-때)
		- [pre\_randomize](#pre_randomize)
	- [Nested Object](#nested-object)
	- [std::randomize(): 교수님께서 권장하시지 않음](#stdrandomize-교수님께서-권장하시지-않음)
	- [Random Seed](#random-seed)
		- [get\_initial\_random\_seed();](#get_initial_random_seed)
- [실습](#실습)
	- [Static member](#static-member)
		- [결과](#결과)
		- [분석](#분석)
		- [비교: static이 없을 때](#비교-static이-없을-때)
			- [결과](#결과-1)
			- [분석](#분석-1)
	- [Keyword This](#keyword-this)
		- [잘못된 코드 예시](#잘못된-코드-예시)
		- [결과](#결과-2)
		- [분석](#분석-2)
		- [해결: this 사용](#해결-this-사용)
			- [결과](#결과-3)
			- [분석](#분석-3)
	- [Shallow copy](#shallow-copy)
		- [결과](#결과-4)
		- [분석](#분석-4)
	- [Random Dynamic Array](#random-dynamic-array)
		- [결과](#결과-5)
		- [분석](#분석-5)
		- [비교: int 배열](#비교-int-배열)
			- [결과](#결과-6)
	- [Array Constraint](#array-constraint-1)
		- [결과](#결과-7)
	- [Pre\_randomize](#pre_randomize-1)
		- [결과](#결과-8)
		- [Question: 랜덤 옵션을 껐는데 randomize() 조건 참인 이유](#question-랜덤-옵션을-껐는데-randomize-조건-참인-이유)
	- [Constraint 조건이 미 충족되는 경우](#constraint-조건이-미-충족되는-경우)
		- [결과](#결과-9)
		- [분석](#분석-6)


# Class
> Class는 어디에 쓰이는가
> > Program에 사용

### Q. Class 왜 쓸까
- ⭐Reusable

## property + method = member
- property: 변수
- Method: Subroutine
- member = property + method

## Class Vs Module

| Class   | Module |
| ------- | ------ |
| Dynamic | Static |

## Class 사용
![alt text](image-38.png)<br>
- pkt1, pkt2: Handle
  - module의 instance 같은 것
- new(): 일종의 function
  - Class는 new를 사용하여 반드시 초기화 해야함
  - 이걸 해야 메모리 할당됨
  - new()는 argument가 있을수도, 없을수도 있음
  - ❗Dynamic array의 new[]와 구분하기!
- handle_name.member(property or method)
  - ex) pkt2.sa = 3
  - 반드시 new()를 통해 메모리 할당 후 member를 사용해야함

### new()
- new()는 base 함수로 class에 대한 메모리를 할당해줌
- 그런데 class 안에 동일한 new이름으로 함수를 만들면 메모리 할당 + class안에 함수 동작이 추가되어 동작

## this(keyword)
> automatic <-> static처럼 **this <-> super** 기억하기

- this
  - this.property
  - 내 class 안에 있는 property를 가리킴
- ![alt text](image-39.png)<br>
- ![alt text](image-51.png)<br>

## local(keyword)
> local <-> protect
> > ⭐local이 protect보다 더 제한이 강함
- 밖에서 class를 접근할 때, property를 접근할 수 없게 낌듦
- C언어의 constant 같은 느낌
  - const는 아예 고정이고, local은 제한을 두고, 일정부분 접근권한을 주는 느낌
- ![alt text](image-41.png)<br>
### function을 사용한 access(argument로 받음)
- ![alt text](image-42.png)<br>

## ⭐Handle 할당(Shallow Copy)
- handle 이름을 할당해도 내용이 같이 복사되지 않음
- 핸들 이름만 옮겨감
- ![alt text](image-43.png)<br>
- ![alt text](image-52.png)<br>
- 이를 Shallow copy라고 함
- 핸들만 옮겨가고 남은 pkt1은 쓰레기 메모리가 됨
  - 해결해줘야함

## static
![alt text](image-44.png)<br>
- static을 사용하면 class의 메모리에 생성되는게 아니라 따로 메모리를 할당 받음

## const(keyword)
![alt text](image-45.png)<br>
- const == read only

## Parameterized Class
### 기존 코드
![alt text](image-46.png)<br>
### 파라미터화한 Class
![alt text](image-47.png)<br>

## typedef class
![alt text](image-48.png)<br>
- 뒤에 온 class를 앞에 class를 포함시킬 때, typedef으로 class를 먼저 정의해야 compile 에러가 없다

## ::
![alt text](image-49.png)<br>
- class가 program이나 다른 내부에서 정의가 안된 경우(task의 body가 없을 때)
- class의 member가 외부에(extern)정의됐을 때 ::으로 접근한다

## Package
- package-endpackage
- package 안에 내용물에 접근할 때
  - ::을 이용함
  - ex) ComplexPKG :: complex cout
    - ComplexPKG: PKG
    - complex: class
    - cout: property
- Package 안에 내용을 사용할 때는 import를 사용하기도 함
- ![alt text](image-50.png)<br>

# Randomization
> Random값을 어떻게 주느냐에 따라 test coverage가 바뀜!!!

## randomize()
- SV에서만 있는 랜덤화 함수
- return value
  - 랜덤화 됨: 1
  - 랜덤화 안됨: 0
### rand
- 주사위
- 한 번 등장한 수가 다시 등장 가능
### randc
- 카드 뽑기
- 한 번 등장한 수는 다시 등장 불가
### 예시
![alt text](image-53.png)<br>
- Dynamic array에 대해 rand함수를 쓰면 배열의 요소가 랜덤으로 설정됨

## Constraint
- 보통 class 안에서 정의해줌
- constraint 이름 {} 형식
- ![alt text](image-54.png)<br>
- constraint 안에 랜덤 제약 조건을 넣어줌

### dist(keyword)
![alt text](image-55.png)<br>
- dist: 확률을 정해줌
- :=
  - 동일한 확률 지정
  - 위의 예시)
    - 5, 6, ,7은 모두 30/110의 확률을 가짐
- :/
  - 확률이 나뉨
  - 위의 예시)
    - 5, 6, 7은 10/50의 확률을 가짐

### Array Constraint
![alt text](image-56.png)<br>
```systemverilog
drivers_in_use.sum() with (int'(item)) == num_of_drivers;
```
- drivers_in_use의 1의 개수가 num_of_drivers의 값과 같은 경우를 값으로 가짐
  - ex)
  - num_of_drivers가 2인 경우
  - drivers_in_use는 'h03,'h0C등과 같이 1의 개수가 2개인 case들을 값으로 가질 수 있음
- ->: System verilog 문법
  - if와 같은 문법
- **item**(keyword)
  - system verilog에서 배열의 요소(content)를 나타내는 키워드
- **with**(조건을 나타내는 keyword)
  - 조건을 나타내는 키워드임

### Implication & Order constraint
1. Implication(->)
![alt text](image-57.png)<br>
- any가 되는 경우 0~255 사이의 값이 됨
2. Equivalence (<->)
![alt text](image-58.png)<br>
- any가 되는 경우 아무것도 할당 안됨
3. Unique
![alt text](image-59.png)<br>
- unique에 정의된 변수끼리는 값이 겹치면 안됨

## System Function
### 

## Solver Order
![alt text](image-60.png)<br>

## Inline constraint
![alt text](image-61.png)<br>
- 다음과 같이 조건이 충돌이 나면
- 두 개의 조건을 모두 만족시키는 조건으로 간다
- 교집합

## Soft Constraint
### soft(keyword)
![alt text](image-62.png)<br>
- soft는 priority가 떨어지는 제약조건이다
- soft 조건과 다른 조건이 겹치지 않는 경우
  - soft조건이 아닌 조건을 따라감
  - **soft: 되면 좋고 안됨 말고**

### 다 soft인 경우
![alt text](image-63.png)<br>
- class 안에 soft보다 inline soft의 제약이 우선순위를 갖는다
  - class 밖에 soft가 우선순위를 가짐

## Mutual Constraint
![alt text](image-64.png)<br>
- high가 random으로 결정되면 Constraint도 high에 영향을 받아 랜덤으로 결정됨
- 랜덤값이 constraint에 영향을 줌
  - 상호 영향 == Mutual

### Error Case
![alt text](image-65.png)<br>

## randomize() 부를 때
- randomize()를 부르면
  - pre_randomize()
  - 변수 랜덤화
  - post_randomize()
- 와 같은 동작이 수행된다
### pre_randomize
- Constraint활성화 여부 결정
- ex)
  - rand_mode(0|1)
  - 0: 랜덤 off
  - 1: 랜덤 on

## Nested Object
![alt text](image-66.png)<br>

## std::randomize(): 교수님께서 권장하시지 않음
- Synopsys 자랑임
- 굳이 권장하진 않음

## Random Seed
![alt text](image-67.png)<br>
![alt text](image-68.png)<br>
- +ntb_random_seed: 랜덤 벡터를 넣어주는 옵션
  - vcs 옵션이 아님
  - simv 옵션임
  - compile 속도를 올려줌
    - 전부 compile하는 것이 아닌 simv만 수정해줌

### get_initial_random_seed();
- 해당 function을 tb에 포함시킴
- simv log파일을 통해 초기 seed 값을 확인
- 추후 simv 옵션에 시드값을 넣을 때, 초기 시드값을 피해 값을 넣어줄 수 있음

# 실습
## Static member
```systemverilog
`timescale 1ns/10ps

typedef class Packet;

program automatic static_member (
	
);
	initial begin
		Packet pkt0 = new();
		Packet pkt1 = new();
		$display("pkt0 id is: %0d", pkt0.id);
		$display("pkt1 id is: %0d", pkt1.id);
		$display("count: %0d", Packet::count);
	end
endprogram : static_member

class Packet;
	static int count = 0;
	int id;
	static function int get_count();
		return count;
	endfunction
	function new();
		this.id = count++;
	endfunction
endclass : Packet
```
### 결과
![alt text](image-69.png)<br>

### 분석
- static으로 property를 정의하면 class의 메모리에 같이 잡히는 것이 아닌, 별개의 메모리로 할당받음
- 때문에 class를 불러올때마다, count가 초기화되지 않고, 이전의 값이 계속 메모리에 유지되어 계속 증가함

### 비교: static이 없을 때
```systemverilog
`timescale 1ns/10ps

typedef class Packet;

program automatic static_member (
	
);
	initial begin
		Packet pkt0 = new();
		Packet pkt1 = new();
		$display("pkt0 id is: %0d", pkt0.id);
		$display("pkt1 id is: %0d", pkt1.id);
		//$display("count: %0d", Packet::count);
	end
endprogram : static_member

class Packet;
	 int count = 0;
	int id;
	 function int get_count();
		return count;
	endfunction
	function new();
		this.id = count++;
	endfunction
endclass : Packet
```
#### 결과
![alt text](image-70.png)<br>

#### 분석
- //$display("count: %0d", Packet::count);
- 이 부분 주석 풀면 에러나는 이유
- class를 불러올 때마다 count 멤버가 따로 생성됨
  - pkt0의 count
  - pkt1의 count
- count가 정적인(static) member가 아니기 때문에 하나의 count에 접근할 수 없음
- static을 사용하게 되면 하나의 count 메모리 공간을 공유함

## Keyword This
### 잘못된 코드 예시
```systemverilog
`timescale 1ns/10ps

program this_keyword (
	
);
	class abc;
		int a = 10;
		function new(int a);
		a = a;
		endfunction
	endclass	

	abc o1;

	initial begin
		o1 = new(5);
		$display("a = %0d", o1.a);
	end
endprogram: this_keyword
```
### 결과
![alt text](image-71.png)<br>
### 분석
- 목적은 class abc의 property a에 5를 넣는 것이 목적임
- 그러나 실제로 function안에 a는 function의 argument를 나타내는 a임
- 즉, a = a가 argument의 a를 의미함
  - abc의 property int a가 아님
- 그래서 값 5가 a에 들어가지 않음

### 해결: this 사용
```systemverilog
`timescale 1ns/10ps

program this_keyword (
	
);
	class abc;
		int a = 10;
		function new(int a);
		this.a = a;
		endfunction
	endclass	

	abc o1;

	initial begin
		o1 = new(5);
		$display("a = %0d", o1.a);
	end
endprogram: this_keyword
```
#### 결과
![alt text](image-72.png)<br>
#### 분석
- this는 class 안에 property를 가리킴
- function 안에 this가 class의 property를 가리키게 됨
  - new(5)에서 인자로 넣어준 5가 function에 들어가서 class property a에 잘 전달됨
- 따라서 원하는 동작대로 a에 5가 나옴

## Shallow copy
```systemverilog
`timescale 1ns/10ps

program automatic shallow_cp (
	
);
	class abc;
		int a;	
	endclass //abc

	initial begin
		abc o1 = new();
		abc o2 = new();

		o1.a = 5;
		o2.a = 50;

		$display("A: %0d %0d", o1.a, o2.a);
		o2 = o1;
		o1.a = 500;
		$display("B: %0d %0d", o1.a, o2.a);
	end	
endprogram: shallow_cp
```
### 결과
![alt text](image-73.png)<br>
### 분석
- o2 = o1
  - o1의 property까지 o2한테 전달 안됨
  - o2의 handle이 o1을 가리키게 됨
  - o2의 member들은 garbage memory가 됨
- o2의 handle이 o1을 가리키므로 o2.a를 찍으면 o1.a가 찍히게 됨
  - 그래서 o2.a가 500이 나옴

## Random Dynamic Array
```systemverilog
class Packet;
	randc bit[3:0] sa, da;
	rand bit[7:0] payload[] = new[5];

	function void display();
		$display("sa = %0d, da = %0d", sa, da);
		$display("size of payload array = %0d", payload.size());
		$display("payload[] = %p", payload);
	endfunction	: display
endclass : Packet

program automatic random_array(
	
);
	initial begin
	Packet pkt = new();
	if(!pkt.randomize()) $finish;
	pkt.display();
	end
endprogram : random_array 
```
### 결과
![alt text](image-74.png)<br>
### 분석
- bit 벡터들과 배열의 요소들이 랜덤한 값으로 나옴
- bit는 unsigned가 default이므로 양수가 나오는 것을 확인

### 비교: int 배열
```systemverilog
class Packet;
	randc bit[3:0] sa, da;
	rand int payload[] = new[5];

	function void display();
		$display("sa = %0d, da = %0d", sa, da);
		$display("size of payload array = %0d", payload.size());
		$display("payload[] = %p", payload);
	endfunction	: display
endclass : Packet

program automatic random_array(
	
);
	initial begin
	Packet pkt = new();
	if(!pkt.randomize()) $finish;
	pkt.display();
	end
endprogram : random_array 
```
#### 결과
![alt text](image-75.png)<br>
- int는 default가 signed임
  - 랜덤값으로 음수도 나올 수 있음

## Array Constraint
```systemverilog
`timescale 1ns/10ps

class Config;
	rand bit[7:0] addrs[10];
	rand bit drivers_in_use[16];
	rand int num_of_drivers, one_addr;
		constraint limit {
			num_of_drivers inside {[1:16]};
			drivers_in_use.sum() with (int'(item)) == num_of_drivers;
			foreach(addrs[idx]) (idx > 0) -> addrs[idx] > addrs[idx-1];
			one_addr inside addrs;
		}

		function display();
			$display("num_of_drivers = %0d", num_of_drivers);	
			$display("drivers_in_use = %p", drivers_in_use);	
			$display("addrs = %p", addrs);
			$display("one_addr = %p", one_addr);
		endfunction	
endclass : Config


program automatic array_const (
	
);
	initial begin
	Config pkt1 = new();
	if(!pkt1.randomize()) $finish;
	pkt1.display();
	end
endprogram
```
### 결과
![alt text](image-76.png)<br>
- Constraint 조건에 맞게 랜덤값이 들어가는 것을 확인할 수 있다
- ->: 조건문
  - num_of_drivers가 7이 할당
  - drivers_in_use는 1이 7개인 모든 case가 가능
  - addrs는 이전 index의 요소보다 큰 값을 가짐
  - one_addr은 addrs의 요소 중에 하나를 가짐

## Pre_randomize
```systemverilog
`timescale 1ns/10ps

class Node;
	randc int x, y, z;
	
	constraint Limit1 {
		x inside {[0:16]};
		y inside {[23:41]};
		z < y; 
		z > x;
	}	
endclass : Node


program automatic pre_rand (
	
);
	initial begin
	Node obj1 = new();
	obj1.x = 0;
	obj1.x.rand_mode(0);
	if(!obj1.randomize()) $finish;
	$display("obj1.x = %0d", obj1.x);
	$display("obj1.y = %0d", obj1.y);
	$display("obj1.z = %0d", obj1.z);
	end
endprogram : pre_rand
```
### 결과
![alt text](image-78.png)<br>
- randomize()를 실행하면
  - pre_randomize()
  - 랜덤 값을 변수에 할당
  - post_randomize()를 수행함
- pre_randomize 단계에서 rand_mode를 0으로 세팅하면 랜덤화를 하지 않음

### Question: 랜덤 옵션을 껐는데 randomize() 조건 참인 이유
- randomization()은 pre-randomize가 수행되도 실행한걸로 판단
- randomization이 안되는 case는 거의 없음
  - 그럼에도 if문을 써서 조건을 걸어주는 이유는
  - 툴이나 컴파일러의 문제로 인해 발생하는 미연의 문제를 방지하기 위함
  - 일말의 문제를 방지

## Constraint 조건이 미 충족되는 경우
```systemverilog
`timescale 1ns/10ps

module automatic n_en_cond (
	
);
	class abc;
		rand int a;
		constraint c1 {a inside {[1:4]};}
		constraint c2 {a inside {[5:8]};}
	endclass

	initial begin
		abc o1 = new();
		o1.randomize();
		$display("test: o1 = %p", o1);
	end
endmodule: n_en_cond
```
### 결과
![alt text](image-79.png)<br>
- a에 0(default value)할당
### 분석
![alt text](image-80.png)<br>
- Error 메시지에서 **Constraint Inconsistency** 문제
  - class의 두개의 constraint에 대해 만족하는 조건이 없음
  - 조건 일관성이 없다는 에러가 나옴
  - a는 조건 하에 랜덤값을 받지 못함
    - default 값 유지
